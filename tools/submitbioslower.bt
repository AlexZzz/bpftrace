#!/usr/bin/env bpftrace
/*
 * submitbioslower	Trace submit_bio events.
 *			For Linux, uses bpftrace and eBPF.
 *
 * USAGE: submitbioslower.bt 100
 *
 * Copyright 2019 Aleksei Zakharov
 * Licensed under the Apache License, Version 2.0 (the "License")
 */


#include <linux/bio.h>
#include <linux/genhd.h>

BEGIN
{
	@threshold = 1000;
	if ($1) {
		@threshold = $1;
	}
	printf("Tracing submit_bio events... Hit Ctrl-C to end.\n");
	printf("Show requests slower than %d usecs.\n",@threshold);
	printf("%-8s %-6s %-16s %-16s %s(us)\n", "TIME", "PID", "COMM", "DEVICE","LAT");
}

kprobe:submit_bio
{
	@start[tid] = nsecs;
	@disk[tid] = arg0
}

kretprobe:submit_bio
/@start[tid]/
{
	$delta = (nsecs-@start[tid])/1000;
	if ($delta > @threshold) {
		time("%H:%M:%S ");
		printf("%-6d %-16s %-16s %ld\n", pid, comm, ((bio *)@disk[tid])->bi_disk->disk_name, $delta);
	}
	delete(@start[tid]);
	delete(@disk[tid]);
}
